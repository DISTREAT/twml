COMMENT = _{ "\\\\" ~ ( !NEWLINE ~ ANY )* }
WHITESPACE = _{ NEWLINE+ }
INDENTATION = _{ ( " " | "\t" )+ }

tailwind_class = _{ ASCII_ALPHANUMERIC+ ~ ( "-" | "/" | "[" | "]" | ASCII_ALPHANUMERIC+ )* }

attributes = _{ "{" ~ " "* ~ attribute ~ ( " "+ ~ attribute )* ~ " "* ~ "}" }
attribute = { attribute_key ~ "=" ~ "\"" ~ attribute_value ~ "\"" }
attribute_key = { ASCII_ALPHA+ }
attribute_value = { ( ( !( "\\" | "\"" ) ~ ANY ) | "\\\"" )* }

document = { COMMENT* ~ declaration* ~ block* ~ EOI }

declaration = ${ "@" ~ declaration_key ~ " "+ ~ declaration_value ~ NEWLINE }
declaration_key = { ( !" " ~ ANY)* }
declaration_value = { ( !NEWLINE ~ ANY )* }

block = @{ block_rules ~ ( NEWLINE ~ block_content_empty_line* ~ PEEK_ALL ~ block_rules )* }
block_rules = { block_element | COMMENT | block_content_line }
block_content_line = ${ ( !NEWLINE ~ ANY )+ }
block_content_empty_line = ${ NEWLINE }

block_children = { PEEK_ALL ~ PUSH(INDENTATION) ~ block ~ DROP }

block_element = ${ "\\" ~ block_element_name ~ block_element_classes? ~ block_element_attributes? ~ ( (" " ~ block_element_content) | ( NEWLINE ~ block_children ) )? }
block_element_name = { ASCII_ALPHA_LOWER+ }
block_element_classes = { ("." ~ block_element_class)+ }
block_element_class = { tailwind_class }
block_element_attributes = { attributes }
block_element_content = { (!NEWLINE ~ ANY)+ }
